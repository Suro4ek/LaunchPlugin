buildscript {
    ext {
        // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-bom
        protobufVersion = '3.19.2'
        // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-gradle-plugin
        protobufPluginVersion = '0.8.18'
        // https://mvnrepository.com/artifact/io.grpc/grpc-bom
        grpcVersion = '1.47.0-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id "com.google.protobuf" version "${protobufPluginVersion}"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'idea'
}


group = 'eu.suro.launch'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.2"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.47.0"
        }
    }
    generatedFilesBaseDir = "$projectDir/src"
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}


sourceSets {
    main {
        proto {
            srcDir "$projectDir/proto"
        }
    }
}

dependencies {
    implementation 'com.destroystokyo.paper:paper-api:1.12.2-R0.1-SNAPSHOT'
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.47.0'
    implementation 'io.grpc:grpc-protobuf:1.47.0'
    implementation 'io.grpc:grpc-stub:1.47.0'
//    implementation "io.grpc:grpc-xds:1.47.0"
    implementation "com.google.protobuf:protobuf-java-util:3.19.2"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

shadowJar {
    mergeServiceFiles()
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
